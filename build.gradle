plugins {
  // Apply the java plugin to add support for Java
  id 'java'

  // Apply the application plugin to add support for building a CLI application.
  id 'application'
    
  id 'eclipse'
}

group = 'photosrenamer'
version = '1.3.0'  
description = """Photos Renamer"""

sourceCompatibility = targetCompatibility = 8

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile group: 'com.drewnoakes', name: 'metadata-extractor', version:'2.8.1'
  compile group: 'com.jgoodies', name: 'jgoodies-looks', version:'2.7.0'
}

application {
  mainClassName = 'photosrenamer.Main'
}


defaultTasks 'build', 'makeDistribution'

task makeDistribution(dependsOn: ['copyToLib', 'copyArtifact', 'copyShellScripts'])

task copyToLib(type: Copy) {
    from configurations.runtime
    into "$buildDir/distribution/lib"
}

task copyArtifact(type: Copy) {
    from "$buildDir/libs"
    into "$buildDir/distribution/lib"
}

task copyShellScripts(type: Copy) {
    from 'src/main/scripts'
    into "$buildDir/distribution"
    include '**/*.cmd'
    include '**/*.sh'
}
